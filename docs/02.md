# Aula 02

Trabalhando com o banco de dados.

## Index

- [Aula 02](#aula-02)
  - [Index](#index)
  - [Conexão com o banco de dados usando Ecto](#conexão-com-o-banco-de-dados-usando-ecto)
    - [Usando o Docker](#usando-o-docker)
    - [Migrations](#migrations)
    - [Inserção de dados](#inserção-de-dados)
      - [Encriptação da senha](#encriptação-da-senha)
      - [Para persistir os dados](#para-persistir-os-dados)
  - [Faxadas](#faxadas)
  - [Rota de criação de usuário](#rota-de-criação-de-usuário)

## Conexão com o banco de dados usando Ecto

O banco de dados em questão é o **Postgres**.

Em linguagens orientada a objeto, ao trabalhar com banco de dados, existe o conceito de ORM, uma ferramenta que mapeia os modelos (classes) para tabelas no banco de dados.

A ideia do Ecto é **mais ou menos** essa, só que, nesse caso, não tem modelos.

### Usando o Docker

1. Criar o container do postgres (mais detalhes na [documentação](https://hub.docker.com/_/postgres))
   
   `docker run --name some-postgres -e POSTGRES_PASSWORD=mysecretpassword -d postgres`
2. Configurar o nome, senha e porta no arquivo [dev.exs](../config/dev.exs) e [test.exs](../config/test.exs) para os mesmos criados na imagem
3. `mix ecto.setup` para fazer verificar a conexão
4. `mix ecto.create` para criar a base de dados

`obs:` Talvez seja necessário iniciar o container do postgres: `docker start [IMAGEM]`


### Migrations

> "É a nossa interface direta com o banco de dados, não na parte inserção de dados em si, mas na definição o banco está."

Para criar uma migration: `mix ecto.gen.migration [nome_da_migration]`.

Os arquivos de migrations são mantidos no diretório priv. Ao executar o comando de criação a CLI já aponta o arquivo de migration criado.

**Exemplo:** Tabela de usuários

``` elixir
  defmodule Rocketpay.Repo.Migrations.CreateUserTable do
    use Ecto.Migration

    def change do
      create table :users do
        add :name, :string
        add :age, :string
        add :email, :string
        add :password_hash, :string
        add :nickname, :string

        timestamps()
      end

      create unique_index(:users, [:email])
      create unique_index(:users, [:nickname])
    end
  end
```

`obs:` O **campo** id é gerado automaticamente pelo ecto. Por padrão, gera no tipo `integer`. Para alterar essa propriedade é preciso fazer uma configuração no arquivo [config.exs](../config/config.exs):

``` elixir
  config :rocketpay, Rocketpay.Repo,
    migration_primary_key: [type: :binary_id],
    migration_foreign_key: [type: :binary_id]
```

O valor do id é definido durante a [inserção dos dados](#inserção-de-dados)

**Comandos:**
* `timestamps()` - cria e controla automaticamente os campos CreatedAt e UpdatedAt.

* `create unique_index(:table, [:index])`


Para executar a migration e, efetivamente, criar a tabela no banco de dados: `mix ecto.migrate`

**Comandos de recuperação do banco:**

* `mix ecto.drop` - **DELETA** o banco


### Inserção de dados

**Schema:** mapeia o dado para a tabela. É muito semelhante à um *Model*, porém um Schema não tem comportamento, é só um mapeamento de dados.

Diretório `lib/rocketpay`. Como exemplo, a inserção de um novo usuário (arquivo [user.ex](../lib/rocketpay/user.ex))

``` elixir
  defmodule Rocketpay.User do
    # Traz as funcionalidades do Ecto para esse módulo
    use Ecto.Schema
    import Ecto.Changeset

    # @primary_key -> variável do módulo para o id do tipo UUID
    @primary_key {:id, :binary_id, autogenerate: true}

    @required_params [:name, :age, :email, :password_hash, :nickname]

    schema "users" do
      field :name, :string
      field :age, :integer
      field :email, :string
      field :password_hash, :string
      field :nickname, :string

      timestamps()
    end

    # Validar e mapear (cast para a tabela) dados
    def changeset(params) do
      %__MODULE__{} # struct (map com nome)
      |> cast(params, @required_params)
      |> validate_required(@required_params)
      |> validate_length(:password_hash, min: 6)
      |> validate_number(:age, greater_than_or_equal_to: 18)
      |> validate_format(:email, ~r/@/) # regex
      |> unique_constraint([:email])
      |> unique_constraint([:nickname])
    end
  end
```

**Resumindo:**

1. `use Ecto.Schema`
2. `import Ecto.Changeset`
3. `schema "tabela"` e definir os campos com os tipos
4. função `changeset` para validar e mapear os dados


#### Encriptação da senha

Primeiro, criar um novo campo virtual no schema para a senha (não encriptada):

``` elixir
  field :password, :string, virtual: true
```

`obs`: `virtual` significa que não vai ser persistido no banco de dados.

Alterar também o `@required_params` para, ao invés de `password_hash`, apenas o campo `password` e a verificação na função `changeset`.

Instalar a biblioteca `Bcrypt` adicionando o código `[:bcrypt_elixir, "~>2.0"]` no arquivo [mix.esx](../mix.exs) e rodando `mix deps.get` no terminal.

Criar uma função para receber a senha digitada e, usando a função `change()` (de `Ecto.Changeset`),trocar a senha pela senha encriptada pelo Bcrypt:

``` elixir
  defp put_password_hash(%Changeset{valid?: true, changes: %{password: password}} = changeset) do
    # Func change() -> recebe um changeset e altera
    change(changeset, Bcrypt.add_hash(password))
  end

  defp put_password_hash(changeset), do: changeset
```

#### Para persistir os dados

O comando `insert`, do próprio elixir (em *Repo*) cuida de persistir os dados. Esse comando recebe por parâmetro um changeset (que vai cuidar das validações):

`%{...} |> Rocketpay.User.changeset() |> Rocketpay.Repo.insert()`

Então, para facilitar o desenvolvimento e não repetir código toda vez que for preciso persistir dados no banco, **criar o módulo Create**:

1. Em `lib/rocketpay`, criar um novo contexto `users`
2. Criar o módulos
``` elixir
  defmodule Rocketpay.Users.Create do
    alias Rocketpay.{Repo, User}

    def call(params) do
      params
      |> User.changeset()
      |> Repo.insert()
    end
  end
```

## Faxadas

> "Concentrar todas as funções no modulo principal"

Uma maneira de deixar métodos visíveis no module *Rocketpay*, que facilita no desenvolvimento e nas chamadas dos métodos (não ter que ficar usando vários `alias`).

No arquivo [rocketpay.ex](../lib/rocketpay.ex), na raíz da pasta `lib`:
``` elixir
defmodule Rocketpay do
  alias Rocketpay.Users.Create, as: UserCreate

  defdelegate create_user(params), to: UserCreate, as: :call
end

```

## Rota de criação de usuário

* Views
  ``` elixir
    defp handle_response({:ok, %User{} = user}, conn) do
      conn
      |> put_status(:created)
      |> render("create.json", user: user)
    end
  ```

  ``` elixir
    defmodule RocketpayWeb.UsersView do
      alias Rocketpay.User

      def render("create.json", %{user: %User{id: id, name: name, nickname: nickname}}) do
        %{
          message: "User created",
          user: %{
            id: id,
            name: name,
            nickname: nickname
          }
        }
      end
    end
  ```